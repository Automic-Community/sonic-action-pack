*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        save_configuration
Test Template     Save Configuration Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ${TargetFile}
Save Configuration with valid Target file path
                      [Tags]    main_scenarios
                      OK        ${Target_File}

Save Configuration with valid non-existing file name but valid path
                      OK        /etc/sonic/NonExistingConfig.json

Save Configuration with invalid file path
                      NOK       Invalid

*** Keywords ***
Startup
    Log    *******Start Test "SAVE CONFIGURATION"*******
    Log    *******Connect to AE*******
    Connect AE

Save Configuration Template
    [Arguments]    ${OK_NOK}    ${TargetFile}
    [Documentation]    This action will save the configuration to a file so that the user can save Config DB content into the file to import in another system.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TargetFile}: This field provides a file name with the full path to save the configuration.
    Generate String
    Init Action    ${SAVE_CONFIGURATION_FILE}
    Connect using SSH
    Run Keyword If    '${TargetFile}' == 'Invalid'    Action Set    &UC4RB_SONIC_TARGET_FILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SONIC_TARGET_FILE#    ${TargetFile}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "SAVE CONFIGURATION"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        add_member_port_to_vlan
Test Template     Add Member Port to VLAN Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VLANID            MemberName        Untagged
Add Member Port to VLAN while untagged is set as True
                      [Tags]    main_scenarios
                      OK        ${VLAN_ID}        ${Member_Name}    True

Add Member Port to VLAN while untagged is set as False
                      OK        ${VLAN_ID_2}      ${Member_Name}    False

Add Member Port to VLAN with invalid vlan id i.e. non integer
                      NOK       Invalid           ${Member_Name}    True

*** Keywords ***
Startup
    Log    *******Start Test "ADD MEMBER PORT TO VLAN"*******
    Log    *******Connect to AE*******
    Connect AE

Add Member Port to VLAN Template
    [Arguments]    ${OK_NOK}    ${VLANID}    ${MemberName}    ${Untagged}
    [Documentation]    This action tests the feature of Add Member Port to VLAN.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VLANID}: This field specify ID of the VLAN to add new member port.
    ...    -${MemberName}: This field specify Member Port name to add to the VLAN.
    ...    -${Untagged}: This field specify whether if member port is untagged or not.
    Generate String
    Init Action    ${ADD_MEMBER_PORT_TO_VLAN_ACTION}
    Connect using SSH
    Run Keyword If    '${VLANID}' == 'Invalid'    Action Set    &UC4RB_SONIC_VLAN_ID#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_VLAN_ID#    ${VLANID}
    Run Keyword If    '${MemberName}' == 'Invalid'    Action Set    &UC4RB_SONIC_MEMBER_PORT#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_MEMBER_PORT#    ${MemberName}
    Action Set    &UC4RB_SONIC_UNTAGGED#    ${Untagged}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "ADD MEMBER PORT TO VLAN"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_member_port_from_vlan
Test Template     Delete Member Port from VLAN Template
Library           com.aufrommic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VLANID            MemberName
Delete Member Port from VLAN
                      [Tags]    main_scenarios
                      OK        ${VLAN_ID}        ${Member_Name}

Delete Member Port from VLAN with invalid vlan id i.e. non integer
                      NOK       Invalid           ${Member_Name}

*** Keywords ***
Startup
    Log    *******Start Test "DELETE MEMBER PORT FROM VLAN"*******
    Log    *******Connect from AE*******
    Connect AE

Delete Member Port from VLAN Template
    [Arguments]    ${OK_NOK}    ${VLANID}    ${MemberName}
    [Documentation]    This action tests the feature of Delete Member Port from VLAN.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VLANID}: This field specify ID of the VLAN from remove new member port.
    ...    -${MemberName}: This field specify Name of the Member Port to be removed from the VLAN.
    Generate String
    Init Action    ${DELETE_MEMBER_PORT_FROM_VLAN_ACTION}
    Connect using SSH
    Run Keyword If    '${VLANID}' == 'Invalid'    Action Set    &UC4RB_SONIC_VLAN_ID#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_VLAN_ID#    ${VLANID}
    Run Keyword If    '${MemberName}' == 'Invalid'    Action Set    &UC4RB_SONIC_MEMBER_PORT#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_MEMBER_PORT#    ${MemberName}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "DELETE MEMBER PORT FROM VLAN"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        load_management_configuration
Test Template     Load Management Configuration Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    MgmtConfigFile
Load Management Configuration from user specified valid file
                      [Tags]    main_scenarios
                      OK        ${Management_Config_File}

Load Management Configuration from default file
                      NOK       ${EMPTY}

Load Management Configuration while providing invalid file path
                      NOK       Invalid

*** Keywords ***
Startup
    Log    *******Start Test "LOAD MANAGEMENT CONFIGURATION"*******
    Log    *******Connect to AE*******
    Connect AE

Load Management Configuration Template
    [Arguments]    ${OK_NOK}    ${MgmtConfigFile}
    [Documentation]    This action will Load Management Configuration from a given file. So that users can apply already prepared configuration to the environment.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${MgmtConfigFile}: This field specifies management configuration file name with full path to load.
    Generate String
    Init Action    ${LOAD_MANAGEMENT_CONFIGURATION}
    Connect using SSH
    Run Keyword If    '${MgmtConfigFile}' == 'Invalid'    Action Set    &UC4RB_SONIC_CONFIG_FILE#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_CONFIG_FILE#    ${MgmtConfigFile}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "LOAD MANAGEMENT CONFIGURATION"*******

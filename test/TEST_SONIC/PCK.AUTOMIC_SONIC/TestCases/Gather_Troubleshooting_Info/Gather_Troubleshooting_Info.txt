*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        gather_troubleshooting_info
Test Template     Gather Troubleshooting Info Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TargetFile
Gather Troubleshooting Info while Target File is Empty
                      [Tags]    main_scenarios
                      OK        ${EMPTY}

Gather Troubleshooting Info while Target File is Provided
                      [Tags]    main_scenarios
                      OK        ${EXECDIR}

Gather Troubleshooting Info while invalid Target File is provided
                      NOK       Invalid

*** Keywords ***
Startup
    Log    *******Start Test "GATHER TROUBLESHOOTING INFO"*******
    Log    *******Connect to AE*******
    Connect AE

Gather Troubleshooting Info Template
    [Arguments]    ${OK_NOK}    ${TargetFile}
    [Documentation]    This action tests the feature of Gather Troubleshooting Info.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TargetFile}: This field specify name with full path to save gathered troubleshooting information.
    Generate String
    Init Action    ${GATHER_TROUBLESHOOTING_INFO_ACTION}
    Connect using SSH
    Run Keyword If    '${TargetFile}' == 'Invalid'    Action Set    &UC4RB_SONIC_TARGET_FILE#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_TARGET_FILE#    ${TargetFile}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Variable Should Be Found    &UC4RB_SONIC_OUTPUT_FILE#    ELSE    Action Variable Should Not Be Found    &UC4RB_SONIC_OUTPUT_FILE#

Teardown
    Log    *******End Test "GATHER TROUBLESHOOTING INFO"*******

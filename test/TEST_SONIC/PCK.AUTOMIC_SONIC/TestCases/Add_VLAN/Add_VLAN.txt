*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        add_vlan
Test Template     Add VLAN Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VLANID
Add VLAN with non existing vlan id
                      [Tags]    main_scenarios
                      OK        104

Add VLAN with invalid vlan id
                      NOK       Invalid

*** Keywords ***
Startup
    Log    *******Start Test "ADD VLAN"*******
    Log    *******Connect to AE*******
    Connect AE

Add VLAN Template
    [Arguments]    ${OK_NOK}    ${VLANID}
    [Documentation]    This action tests the feature of Add VLAN.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VLANID}: This field specify ID of the VLAN to be created.
    Generate String
    Init Action    ${ADD_VLAN_ACTION}
    Connect using SSH
    Run Keyword If    '${VLANID}' == 'Invalid'    Action Set    &UC4RB_SONIC_VLAN_ID#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_SONIC_VLAN_ID#    ${VLANID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "ADD VLAN"*******
